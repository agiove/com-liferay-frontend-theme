import com.liferay.gradle.plugins.node.tasks.PublishNodeModuleTask
import com.liferay.gradle.util.copy.StripPathSegmentsAction

configurations {
	alloyUI
	lexicon
}

task buildAlloyUITheme(type: Copy)
task buildLexicon(type: Copy)
task buildLexiconFonts(type: Copy)
task buildLexiconIcons(type: Copy)
task buildTheme
task publishNodeModule(type: PublishNodeModuleTask)

String alloyUIVersion = "3.1.0-deprecated.23"

File themeDestinationDir = file("src/main/resources/META-INF/resources/_unstyled");

buildAlloyUITheme {
	File alloyThemeDestinationDir = new File(themeDestinationDir, "images/aui")

	doFirst {
		delete alloyThemeDestinationDir
	}

	eachFile new StripPathSegmentsAction(5)

	filesMatching "**/build/aui-*/assets/skins/sam/", new StripPathSegmentsAction(5)
	filesMatching "**/build/aui-skin-deprecated/images/", new StripPathSegmentsAction(3)

	from {
		zipTree(configurations.alloyUI.singleFile)
	}

	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-*/assets/skins/sam/**.gif"
	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-*/assets/skins/sam/**.jpg"
	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-*/assets/skins/sam/**.png"
	include "META-INF/resources/webjars/alloy-ui/${alloyUIVersion}/build/aui-skin-deprecated/images/"

	includeEmptyDirs = false
	into alloyThemeDestinationDir
}

buildCSS {
	dependsOn buildTheme
}

buildLexicon {
	dependsOn npmInstall

	File lexiconDestinationDir = new File(themeDestinationDir, "css/clay")

	doFirst {
		delete lexiconDestinationDir
	}

	eachFile {
		if (name in ["atlas.scss", "bootstrap.scss", "base.scss"]) {
			filter {
				String line ->

				if (line == "// INSERT CUSTOM EXTENSIONS") {
					line = """\
@import \"variables\";

@import \"../liferay_variables_custom\";
@import \"../liferay_variables\";
@import \"bourbon\";
@import \"../clay_custom\";
@import \"../liferay_custom\";"""
				}
				else if (line == "// INSERT CUSTOM VARS") {
					line = """\
@import url(font-awesome.css);
@import \"../clay_variables\";"""
				}

				return line
			}
		}
		else if (name in ["atlas-variables.scss", "base-variables.scss"]) {
			filter {
				String line ->

				if (line == "// INSERT CUSTOM BASE VARS") {
					line = """\
@import \"../clay_variables\";"""
				}
				else if (line == "// INSERT CUSTOM VARS") {
					line = """\
@import \"variables\";

@import \"../liferay_variables_custom\";
@import \"../liferay_variables\";"""
				}

				return line
			}
		}
	}

	eachFile new StripPathSegmentsAction(3)

	from npmInstall.nodeModulesDir
	include "clay/src/scss/"
	includeEmptyDirs = false
	into lexiconDestinationDir
}

buildLexiconFonts {
	dependsOn npmInstall

	File lexiconFontsDestinationDir = new File(themeDestinationDir, "css/clay/fonts")

	dependsOn buildLexicon

	doFirst {
		delete lexiconFontsDestinationDir
	}

	eachFile new StripPathSegmentsAction(3)

	from npmInstall.nodeModulesDir
	include "clay/release/fonts/"
	includeEmptyDirs = false
	into lexiconFontsDestinationDir
}

buildLexiconIcons {
	dependsOn npmInstall

	File lexiconIconsDestinationDir = new File(themeDestinationDir, "images/lexicon")

	doFirst {
		delete lexiconIconsDestinationDir
	}

	eachFile new StripPathSegmentsAction(4)

	from npmInstall.nodeModulesDir
	include "clay/release/images/icons/"
	includeEmptyDirs = false
	into lexiconIconsDestinationDir
}

buildTheme {
	dependsOn buildAlloyUITheme
	dependsOn buildLexiconFonts
	dependsOn buildLexiconIcons
}

classes {
	dependsOn buildTheme
}

dependencies {
	alloyUI group: "com.liferay.webjars", name: "com.liferay.webjars.alloy-ui", transitive: false, version: alloyUIVersion
}

publishNodeModule {
	dependsOn jar
	workingDir = "src/main/resources/META-INF/resources/_unstyled"
}